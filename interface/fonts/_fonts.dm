/// A font datum, it exists to define a custom font to use in a span style later.
/datum/font
	/// Font name, just so people know what to put in their span style.
	var/name
	/// The font file we link to.
	var/font_family
	/// Font features and metrics
	/// Generated by Lummox's dmifontsplus (https://www.byond.com/developer/LummoxJR/DmiFontsPlus)
	var/list/metrics
	var/height      	// total height of a line
	var/ascent      	// distance above baseline (including whitespace)
	var/descent     	// distance below baseline
	var/avgwidth    	// average character width
	var/maxwidth    	// maximum character width
	var/overhang    	// extra width, such as from italics, for a line
	var/inleading   	// internal leading vertical space, for accent marks
	var/exleading   	// external leading vertical space, just plain blank
	var/defchar     	// default character (for undefined chars)
	var/start       	// first character in metrics
	var/end				// last character in metrics

// This needs to be updated to /tg/ standards
/// Get font metrics
/// From Lummox's dmifontsplus (https://www.byond.com/developer/LummoxJR/DmiFontsPlus)
/datum/font/proc/get_metrics(text, flags=0, firstline=0)
	.=0
	var/longest=0
	if(!length(text)) return
	var/i=1
	var/idx
	while(i<=length(text))
		var/ch=text2ascii(text,i++)
		if(ch<=10)
			if(ch<=7) .+=ch		// spacers for justification
			if(ch<=9) continue	// soft-break chars
			if(. && idx && !(flags&INCLUDE_AC)) .-=max(metrics[idx+3],0)
			longest=max(longest,.+firstline)
			.=0
			firstline=0
			idx=0
			continue
		idx=(ch-start)*3
		if(idx<=0 || idx>=metrics.len) idx=(defchar-start)*3
		if(!. && !(flags&INCLUDE_AC)) .-=metrics[idx+1]
		.+=metrics[idx+1]+metrics[idx+2]+metrics[idx+3]
	if(. && idx && !(flags&INCLUDE_AC)) .-=max(metrics[idx+3],0)
	.=max(.+firstline,longest)
	if(.>0) .+=overhang
