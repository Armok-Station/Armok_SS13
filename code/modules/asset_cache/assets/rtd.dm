/datum/asset/spritesheet/rtd
	name = "rtd"

/datum/asset/spritesheet/rtd/create_spritesheets()
	for(var/main_root in GLOB.floor_designs)
		for(var/sub_category in GLOB.floor_designs[main_root])
			for(var/list/design in  GLOB.floor_designs[main_root][sub_category])
				var/obj/item/stack/tile/type = design["type"]
				var/icon_state = initial(type.icon_state)
				Insert(sprite_name = icon_state, I = 'icons/obj/tiles.dmi', icon_state = icon_state)

				var/list/tile_directions = design["tile_rotate_dirs"]
				if(tile_directions == null)
					continue

				for(var/direction as anything in tile_directions)
					var/icon/img = icon(icon = 'icons/obj/tiles.dmi', icon_state = icon_state)
					switch(direction)
						if(NORTH)
							img.Turn(-180)
						if(WEST)
							img.Turn(90)
						if(EAST)
							img.Turn(-90)
						if(NORTHEAST)
							img.Turn(-180)
							var/icon/east_rotated = icon(icon = 'icons/obj/tiles.dmi', icon_state = icon_state)
							east_rotated.Turn(-90)
							img.Blend(east_rotated,ICON_MULTIPLY)
							img.SetIntensity(2,2,2)
						if(NORTHWEST)
							img.Turn(-180)
							var/icon/west_rotated = icon(icon = 'icons/obj/tiles.dmi', icon_state = icon_state)
							west_rotated.Turn(90)
							img.Blend(west_rotated,ICON_MULTIPLY)
							img.SetIntensity(2,2,2)
						if(SOUTHEAST)
							var/icon/east_rotated = icon(icon = 'icons/obj/tiles.dmi', icon_state = icon_state)
							east_rotated.Turn(-90)
							img.Blend(east_rotated,ICON_MULTIPLY)
							img.SetIntensity(2,2,2)
						if(SOUTHWEST)
							var/icon/west_rotated = icon(icon = 'icons/obj/tiles.dmi', icon_state = icon_state)
							west_rotated.Turn(90)
							img.Blend(west_rotated,ICON_MULTIPLY)
							img.SetIntensity(2,2,2)
					Insert(sprite_name = "[icon_state]-[dir2text(direction)]", I = img)
